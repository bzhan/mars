%type: module
module SchedulerHPF(sid):
output prior, run_prior;
begin
Pool_prior := [];
Pool_now := [];
GetMaxList := [];
prior := -1;
run_now := "";
run_prior := -1;
{
reqProcessor[sid][_tid]?prior -->
    if (run_prior >= prior) {
        Pool_prior := push(Pool_prior, prior);
        Pool_now := push(Pool_now, _tid);
    } else {
        if (run_now != "") {
            preempt[sid][run_now]!;
        } 
        run_now := _tid;
        run_prior := prior;
        run[sid][run_now]!;
    }
$
free[sid][_tid]? -->
    assert(_tid == run_now);
    if (len(Pool_prior) > 0) {
        run_prior := -1;
        run_now := "";
        max_index := 0;
        index := 0;
        {   
            if (Pool_prior[index] > run_prior) {
                run_prior := Pool_prior[index];
                run_now := Pool_now[index];
                max_index := index;
            }
            index := index + 1;
        } * (index < len(Pool_prior))

        Pool_prior := del(Pool_prior, max_index);
        Pool_now := del(Pool_now, max_index);
        run[sid][run_now]!;
    } else {
        run_prior := -1; run_now := "";
    }
$
exit[sid][_tid]? --> 
    index := 0;
    del_index := 0;
    {   
        if (Pool_now[index] == _tid) {
            del_index := index;
        }
        index := index + 1;
    } * (index < len(Pool_prior))
    Pool_prior := del(Pool_prior, del_index);
    Pool_now := del(Pool_now, del_index);
}*

end
endmodule