constants
    Qmax == 2;
    g == 9.8;
    pi == 3.14;
    r == 0.18;
end

pre [t == 0][h == 4.5][cmd == 1];

{
    t := 0; 
    if (cmd == 1){
        {h_dot = Qmax - pi * r^2 * (2 * g * h)^(1/2), t_dot = 1 & t < 0.05 && h > 0}
	invariant [cmd == 1]
	          [h <= 5.7 + 2 * t]
              [h >= 4]
              [h <= 5.7 + 1.1 * t];
    }
    else {
        {h_dot = - pi * r^2 * (2 * g * h)^(1/2), t_dot = 1 & t < 0.05 && h > 0}
	invariant [cmd == 0]
	          [h <= 6]
              [h >= 4.3 - 1.2 * t];
    }

    h1 := h;
    t := 0;

    if (cmd == 1){
        {h_dot = Qmax - pi * r^2 * (2 * g * h)^(1/2), t_dot = 1 & t < 0.05 && h > 0}
	invariant [cmd == 1]
              [h1 <= 5.8]
              [h1 >= 4]
              [h <= h1 + 2 * t]
              [h >= h1];
    }
    else {
        {h_dot = - pi * r^2 * (2 * g * h)^(1/2), t_dot = 1 & t < 0.05 && h > 0}
	invariant [cmd == 0]
              [h1 >= 4.2]
              [h1 <= 6]
              [h <= h1]
              [h >= h1 - 1.2 * t];
    }

    if (h1 <= 4.4) {
        cmd := 1;
    }
    if (h1 >= 5.6) {
        cmd := 0;
    }
}* invariant [cmd == 1 && h >= 4 && h <= 5.7 || 
              cmd == 0 && h <= 6 && h >= 4.3];

post [h >= 4][h <= 6];
