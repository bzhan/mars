package software
public
	-- obstacle detector
	process obs_det
		features
			img: in data port;
			obs_pos_radar: in data port;
			obs_pos: out data port;
	end obs_det;
	
	process implementation obs_det.imp
		subcomponents
			img_acq_imp: thread img_acq.imp;
			comp_obs_pos_imp: thread comp_obs_pos.imp;
		connections
			tran_img: port img -> img_acq_imp.img;
			tran_proc_img: port img_acq_imp.proc_img -> comp_obs_pos_imp.proc_img;
			tran_radar_obs_pos: port obs_pos_radar -> comp_obs_pos_imp.obs_pos_radar;
			tran_obs_pos: port comp_obs_pos_imp.obs_pos -> obs_pos;
	end obs_det.imp;
	
	-- image acquisition
	thread img_acq
		features
			img: in data port;
			proc_img: out data port;
		properties
			dispatch_protocol => (periodic);
			period => 45ms;
			deadline => 45ms;
			compute_execution_time => 10ms;
			priority => 1;
	end img_acq;
	
	thread implementation img_acq.imp
		annex Simulink{** ./Examples/AADL/CCS/Simulink/img_acq_imp.xml **};
	end img_acq.imp;
	
	-- compute obstacle position
	thread comp_obs_pos
		features
			proc_img: in data port;
			obs_pos_radar: in data port;
			obs_pos: out data port;
		properties
			dispatch_protocol => (periodic);
			period => 97ms;
			deadline => 97ms;
			compute_execution_time => 20ms;
			priority => 1;
	end comp_obs_pos;
	
	thread implementation comp_obs_pos.imp
		annex Simulink{** ./Examples/AADL/CCS/Simulink/comp_obs_pos_imp.xml **};
	end comp_obs_pos.imp;
	
	-- velocity controller
	process velo_ctr
		features
			obs_pos: in data port;
			veh_pos: in data port;
			laser_valid: in data port;
			laser_v: in data port;
			wheel_valid: in data port;
			wheel_v: in data port;
			des_v: in data port;
			cmd: out data port;
	end velo_ctr;
	
	process implementation velo_ctr.imp
		subcomponents
			emerg_imp: thread emerg.imp;
			velo_voter_imp: thread velo_voter.imp;
			PI_ctr_imp:thread PI_ctr.imp;
		connections
			tran_obs_pos: port obs_pos -> emerg_imp.obs_pos;
			tran_cmd: port emerg_imp.cmd -> cmd;
			tran_veh_pos: port veh_pos -> emerg_imp.veh_pos;
			tran_veh_v: port velo_voter_imp.veh_v -> emerg_imp.veh_v;
			tran_des_v: port des_v -> PI_ctr_imp.des_v;
			tran_laser_valid: port laser_valid -> velo_voter_imp.laser_valid;
			tran_laser_v: port laser_v -> velo_voter_imp.laser_v;
			tran_wheel_valid: port wheel_valid -> velo_voter_imp.wheel_valid;
			tran_wheel_v: port wheel_v -> velo_voter_imp.wheel_v;
			tran_veh_v1: port velo_voter_imp.veh_v -> PI_ctr_imp.veh_v;
			tran_des_a: port PI_ctr_imp.des_a -> emerg_imp.des_a;
	end velo_ctr.imp;

	-- discrete PI controller
	thread PI_ctr
	    features
	        veh_v: in data port;
	        des_v: in data port;
	        des_a: out data port;
	    properties
	        dispatch_protocol => periodic;
			period => 7ms;
			deadline => 7ms;
			compute_execution_time => 1ms;
			priority => 1;
	end PI_ctr;

	thread implementation PI_ctr.imp
	    annex Simulink{** ./Examples/AADL/CCS/Simulink/PI_ctr_imp.xml **};
	end PI_ctr.imp;
	
	-- compute acceleration
	thread emerg
		features
			obs_pos: in data port;
			veh_pos: in data port;
			veh_v: in data port;
			des_a: in data port;
			cmd: out data port;
		properties
			dispatch_protocol => periodic;
			period => 5ms;
			deadline => 5ms;
			compute_execution_time => 1ms;
			priority => 2;
	end emerg;
	
	thread implementation emerg.imp
		annex Stateflow{** ./Examples/AADL/CCS/Simulink/emerg_imp.xml **};
	end emerg.imp;
	
	-- velocity voter
	thread velo_voter
		features
			laser_valid: in data port;
			laser_v: in data port;
			wheel_valid: in data port;
			wheel_v: in data port;
			veh_v: out data port;
		properties
			dispatch_protocol => periodic;
			period => 8ms;
			deadline => 8ms;
			compute_execution_time => 1ms;
			priority => 1;
	end velo_voter;
	
	thread implementation velo_voter.imp
		annex Simulink{** ./Examples/AADL/CCS/Simulink/velo_voter_imp.xml **};
	end velo_voter.imp;
	
	-- panel controller
	process pan_ctr
		features
			event: in event port;
			des_v: out data port;
	end pan_ctr;
	
	process implementation pan_ctr.imp
		subcomponents
			pan_ctr_th_imp: thread pan_ctr_th.imp;
		connections
			tran_event: port event -> pan_ctr_th_imp.event;
			tran_des_v: port pan_ctr_th_imp.des_v -> des_v;
	end pan_ctr.imp;
	
	-- panel controller thread
	thread pan_ctr_th
		features
			event: in event port;
			des_v: out data port;
		properties
			dispatch_protocol => (aperiodic);
			compute_execution_time => 10ms;
			deadline => 100ms;
	end pan_ctr_th;
	
	thread implementation pan_ctr_th.imp
		annex Stateflow{** ./Examples/AADL/CCS/Simulink/pan_ctr_th_imp.xml **};
	end pan_ctr_th.imp;
end software;