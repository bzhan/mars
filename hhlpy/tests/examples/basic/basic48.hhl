# ArchiveEntry "Benchmarks/Basic/STTT Tutorial: Example 7"
# 
# Description "A hybrid car model with non-deterministic braking characteristics. The requirement to prove is that the car never runs a stop sign.".
# Citation "Jan-David Quesel, Stefan Mitsch, Sarah Loos, Nikos Aréchiga, and André Platzer. How to model and prove hybrid systems with KeYmaera: A tutorial on safety. STTT, 18(1), pp. 67-91, 2016.".
# Link "http://symbolaris.com/pub/KeYmaera-tutorial.pdf#page=15".

pre [v >= 0] [A > 0] [B >= b] [b > 0] [x+v^2/(2*b) <= S] [ep > 0];
{
  {
    if (x+v^2/(2*b) + (A/b+1)*(A/2*ep^2+ep*v) <= S) {
      a := *(-B <= a && a <= A); 
    } else { 
      a := *(-B <= a && a <= -b); 
    } 
  }
  ++
  { 
    if (v == 0) {
      a := 0;
    } else { 
      a := *(-B <= a && a <= -b);
    }
  }
  ++ 
  { 
    a := *(-B <= a && a <= -b);
  }

  c := 0;
  t := * (t >= 0);
  { x_dot = v, v_dot = a, c_dot = 1, t_dot = -1 &
    t > 0 && v > 0 && c < ep } solution;
}* 
invariant [x+v^2/(2*b) <= S] {{maintain 1(1).exec: wolfram, maintain 1(2).exec: wolfram, 
                               maintain 2(1).exec: wolfram, maintain 2(2).exec: wolfram, 
                               maintain 3.exec: wolfram}}
          [v >= 0];
post [x <= S];